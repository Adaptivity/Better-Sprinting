buildscript{
    repositories{
        jcenter()
        maven{
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    
    dependencies{
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft{
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "snapshot_20151128"
}

processResources{
    File info = new File(sourceSets.main.resources.srcDirs.iterator().next(), "mcmod.info")
    def slurp = new groovy.json.JsonSlurper()
    def data = slurp.parseText(info.getText())[0]

    archivesBaseName = data.name.replaceAll(" ", "")
    version = data.version
}

jar{
    archiveName = archivesBaseName + '-' + project.minecraft.version + '-v' + version + '.jar'
    
    from('./'){
        include 'LICENSE'
    }
    
    manifest{
        attributes 'FMLCorePlugin': 'chylex.bettersprinting.system.core.BetterSprintingCore'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
        attributes 'FMLAT': 'bs_at.cfg'
    }
}

project.getTasks().getByName("runClient").jvmArgs(["-Dfml.coreMods.load=chylex.bettersprinting.system.core.BetterSprintingCore"])
