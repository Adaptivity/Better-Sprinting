configurations{
    shade
    compile.extendsFrom shade
}

buildscript{
    repositories{
        jcenter()
        maven{
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    
    dependencies{
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

dependencies{
    shade files('libs/JavaCheckerShade  MC-UNIVERSAL  v2.0.jar')
}

apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft{
    version = "1.11.2-13.20.1.2588"
    runDir = "run"
    mappings = "snapshot_20170612"
    makeObfSourceJar = false
}

processResources{
    File info = new File(sourceSets.main.resources.srcDirs.iterator().next(), "mcmod.info")
    def slurp = new groovy.json.JsonSlurper()
    def data = slurp.parseText(info.getText())[0]

    archivesBaseName = data.name.replaceAll(" ", "")
    version = data.version
}

reobf{
    jar{
        extraLines "PK: chylex/javacheck bsm/javacheck"
    }
}

jar{
    archiveName = archivesBaseName + '-' + project.minecraft.version + '-v' + version + '.jar'
    
    configurations.shade.each{ dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
    
    from('./'){
        include 'LICENSE'
    }
    
    manifest{
        attributes 'FMLAT': 'bs_at.cfg'
        attributes 'TweakClass': 'bsm.javacheck.Java8Checker'
    }
}
